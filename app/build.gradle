apply plugin: 'com.android.application'

if (project.file('../ActivityDiarySigning.properties').exists()) {
    apply plugin: 'com.github.triplet.play'

    Properties props = new Properties()
    props.load(new FileInputStream(file('../ActivityDiarySigning.properties')))

    android {
        signingConfigs {
            gplayRelease {
                storeFile file("${System.properties['user.home']}${File.separator}/.android_keystore")
                storePassword props['keystore.password']
                keyAlias 'key0'
                keyPassword  props['key.password']
            }
        }
        playAccountConfigs {
            defaultAccountConfig {
                serviceAccountEmail = 'gplay-ramack@api-5226730847504381805-103625.iam.gserviceaccount.com'
                pk12File = file('../gplay-developer-9607db552238.p12')
            }
        }        
    }
    play {
        uploadImages = true
//        track = 'beta'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "de.rampro.activitydiary"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 4
        versionName "0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "ActivityDiary-" + defaultConfig.versionName)
        jackOptions {
            enabled true
        }
        if (project.file('../ActivityDiarySigning.properties').exists()) {
            playAccountConfig = playAccountConfigs.defaultAccountConfig
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        gplayRelease {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.file('../ActivityDiarySigning.properties').exists()) {
                signingConfig signingConfigs.gplayRelease
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:preference-v7:${supportLibVersion}"
    compile "com.android.support:preference-v14:${supportLibVersion}"
    compile 'com.pes.materialcolorpicker:library:1.2.2'
    compile 'ch.acra:acra:4.9.2'

    testCompile 'junit:junit:4.12'
}
